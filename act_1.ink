=== Act1_Scene1_Brothers_Departure ===
#Location: Izba
#clear
Тяжелый запах дыма и кислой овчины. Старшие братья, сопя, затягивают ремни на тюках. Один лишь Емеля не участвует в суете. Он сидит на теплой печи и вертит в руках гладко обточенную деревяшку с лопастями.
Старший брат: (строго) Емеля, отца схоронили, а ты все дурью маешься. Мы в город. Хозяйство на тебе. Слушайся невесток.
Средний брат: (мягче) Вернемся — купим тебе кафтан красный, шапку, сапоги.
Невестка: (вздыхая) Емеля, хоть бы за водой сходил. В котле пусто.

* [Потребовать награду]
    ~ score_classic += 1
    Емеля: "А кафтан точно купите?" #Н
    Средний брат усмехается. "Слово купеческое! Будет тебе кафтан."
* [Найти и решить проблему]
    ~ score_inventor += 1
    Емеля спрыгивает с печи, указывает на щель в ведре.
    Емеля: "Так оно дырявое." #Н
    Старший брат хмурится: "Не умничай. Другого нет."
    Емеля: "И пойду." #Р
    Он выходит на крыльцо, зачерпывает снега, забивает щель и плещет воды. Ледяная "заплатка" схватывается на морозе.
    Емеля: "Готово. На один раз хватит." #Н
    Братья и невестки смотрят на него со смешанным чувством удивления и досады.
* [Пошутить]
    ~ score_storyteller += 1
    Емеля: "Так я ж с печи если упаду, весь дом развалю." #Р
    Невестка лишь фыркает: "Пустомеля".
-
Братья уходят. Тяжелая дверь за ними закрывается. Емеля берет вёдра и топор, накидывает тулуп и выходит на мороз.
-> Act1_Scene2_The_River


=== Act1_Scene2_The_River ===
#Location: River_Winter
#clear
Холод тут же впивается под тулуп. Снег под валенками скрипит зло и колко. Каждый удар топора по толстому льду отдается тупой болью в замерзших руках.
Наконец, прорубь готова. Он зачерпывает воды, как вдруг в проруби сверкает чешуей огромная щука. Емеля, не раздумывая, хватает ее.
Щука: Отпусти, Емеля! На что я тебе?

* [Ответить по-простому]
    ~ score_classic += 1
    Емеля: "Как на что? На уху. Знатная будет." #Н
    Щука: "Не ешь меня. Отпусти, а я сделаю так, что любое твое желание исполнится."
    Емеля: "Словам я не верю. Сперва докажи." #Н
* [Попытаться понять]
    ~ score_inventor += 1
    Емеля: "Как... ты говоришь?" #У
    Щука: "Долгая это история. Отпусти, и узнаешь вещи почудеснее. Я сделаю так, что любое твое желание исполнится."
    Емеля: "Предложение твое занятное. Но требует проверки. Докажи." #Н
* [Начать диалог]
    ~ score_storyteller += 1
    Емеля: "А о чем еще рыбы в реке говорят? Не о том ли, как зима холодна?" #Р
    Щука: (смеется) "Говорлив ты, Емеля. Отпусти, и я дам тебе силу творить свои собственные истории. Любое твое желание исполнится."
    Емеля: "Красиво говоришь. А теперь покажи делом." #Р
-
Щука: "Хорошо. Помни слова: 'По щучьему веленью, по моему хотенью...'. А теперь пожелай чего-нибудь."
-> Act1_Scene3_Buckets


=== Act1_Scene3_Buckets ===
// Локация остается River_Winter
Емеля решает проверить слова щуки.
* [Приказать просто]
    ~ score_classic += 1
    ~ magic_distortion += 3
    Емеля: "По щучьему веленью... ступайте, вёдра, сами домой!" #Н
    -> buckets_go
* [Дать точное задание]
    ~ score_inventor += 1
    ~ magic_distortion += 1
    Емеля: "По щучьему веленью... вёдра, доставьте воду в избу, не расплескав." #Н
    -> buckets_go
* [Обратиться с просьбой]
    ~ score_storyteller += 1
    ~ magic_distortion += 2
    Емеля: "По щучьему веленью... вёдра-молодцы, отнесите водицу-сестрицу!" #Р
    -> buckets_go

= buckets_go
Когда вёдра тронулись с места, Емеля почувствовал легкую щекотку за ушами, словно паутинка коснулась. Он отмахнулся, но ничего не было.
Емеля отпускает щуку.
#Location: Village_Winter
#clear
{
- score_classic > score_inventor and score_classic > score_storyteller:
    Вёдра с оглушительным стуком подпрыгивают и, распугивая ворон и деревенских баб, с грохотом несутся в гору.
- score_inventor > score_classic and score_inventor > score_storyteller:
    Емеля с любопытством наблюдает, как из днища вёдер выдвигаются шарнирные "утиные лапки". Механизм оживает, и вёдра аккуратно шагают к дому.
- score_storyteller > score_classic and score_storyteller > score_storyteller:
    Вёдра оживают, на их боках проступают смешные рожицы и, напевая тоненькими голосами, они идут вприсядку.
- else:
    Вёдра просто едут сами.
}
Емеля идет следом. Народ на улице, видя такое диво, кто в страхе крестится, кто в избу прячется.
Он приходит домой, а вёдра уже стоят на лавке. Емеля снова забирается на теплую печь.
-> Act1_Scene4_Axes


=== Act1_Scene4_Axes ===
#Location: Izba
#clear
Невестка: "Емеля, что ты лежишь? Дрова-то на исходе, печь скоро остынет!"
* [Исполнить по-щучьему веленью] -> use_magic_axes
* [Слезть и нарубить самому]
    Емеля: (вздыхая) "Ладно, ладно, иду..." #П
    Невестки удивленно переглядываются. Емеля, кряхтя, слезает с печи, берет топор и идет на двор.
    -> Act1_Scene5_Sled

= use_magic_axes
    * [Приказать просто]
        ~ score_classic += 1
        ~ magic_distortion += 4
        Емеля: "По щучьему веленью... топор, поди наруби дров, а вы, дрова, сами в избу идите и в печь кладитесь!" #Н
        Топор сам выпрыгивает из-под лавки и вылетает на двор. Слышится яростный стук. Через мгновение поленья сами "заходят" в избу и запрыгивают в печь.
        Невестки вскрикивают и ахают.
    * [Дать топору точные указания]
        ~ score_inventor += 1
        ~ magic_distortion += 2
        Емеля: "По щучьему веленью... топор, наколи дров. Дрова, сложитесь в поленницу у избы." #Н
        Топор вылетает на двор. Слышится методичный, размеренный стук. Выглянув в окошко, Емеля видит, как топор аккуратно раскалывает поленья, а те сами укладываются в идеальную поленницу.
        Невестки, заглянув в окно, качают головами в изумлении.
    * [Устроить представление]
        ~ score_storyteller += 1
        ~ magic_distortion += 3
        Емеля: "По щучьему веленью... топор-плясун, да дрова-веселушки, устройте-ка нам потеху!" #Р
        Топор вылетает на двор и начинает плясать, отбивая лезвием задорную дробь по поленьям.
        Невестки, выглянув в окно, начинают смеяться.
    -
    Когда топор закончил работу, в избе на мгновение сама собой звякнула печная заслонка, словно от сквозняка.
    -> Act1_Scene5_Sled


=== Act1_Scene5_Sled ===
#Location: Courtyard
#clear
Невестки: "Емеля, дров-то твоих на один раз хватит. В лес надо ехать."
Емеля выходит на двор и садится в пустые сани.
Невестка: "Ты что, дурак, удумал? А лошадь запрячь?!"
* [Ответить прямо]
    ~ score_classic += 1
    Емеля: "Не надо мне лошади." #Н
* [Ответить по-простому, но с хитрецой]
    ~ score_inventor += 1
    Емеля: "Сани у меня нынче сами бегают." #Н
* [Отшутиться]
    ~ score_storyteller += 1
    Емеля: "Сами доедут, не заблудятся." #Р
-
Невестки, пожимая плечами, отворяют ворота.
Емеля: "По щучьему веленью... ну-ка, сани, ступайте в лес!" #Н
{
- score_classic > score_inventor and score_classic > score_storyteller:
    ~ magic_distortion += 4
    Сани с оглушительным треском срываются с места, как пушечное ядро.
- score_inventor > score_classic and score_inventor > score_storyteller:
    ~ magic_distortion += 2
    Емеля замечает, как под полозьями на мгновение формируются гладкие ледяные дорожки.
- score_storyteller > score_classic and score_storyteller > score_storyteller:
    ~ magic_distortion += 3
    Сани подпрыгивают, весело скрипят, а из-под полозьев сыплются разноцветные искры.
- else:
    ~ magic_distortion += 3
    Сани просто едут сами.
}
Емеля замечает, как иней на заборе рядом с ними на мгновение оплавился и тут же замерз снова.
-> Act1_Scene6_Town


=== Act1_Scene6_Town ===
#Location: Town_Street
#clear
Сани влетают на улицы ближайшего города. Емеля, увлеченный скоростью, случайно опрокидывает лоток бедной старушки-торговки, рассыпая по снегу красные яблоки.
VAR helped_old_woman = false
VAR used_magic_on_old_woman = false
* [Проехать мимо]
    Емеля лишь хмыкает и погоняет сани дальше.
* [Остановиться и помочь]
    ~ helped_old_woman = true
    Емеля с досадой останавливает сани. Собирать липкие от снега яблоки — занятие нудное, но старушка бормочет слова благодарности.
* [Извиниться и сотворить чудо]
    ~ used_magic_on_old_woman = true
    ~ magic_distortion += 1
    Емеля: "Прости, бабушка! По щучьему веленью... яблоки, сами в корзину запрыгните!" #П
    Яблоки, подпрыгивая, сами залетают в корзину. Старушка ахает, не веря своим глазам.
-
Торговец с соседней лавки бежит к дому городского старосты с доносом.
-> Act1_Scene7_Baton


=== Act1_Scene7_Baton ===
#Location: Forest_Edge
#clear
Нарубив в лесу дров, Емеля едет обратно. На выезде из города его уже ждет отряд стражников во главе с седобородым старостой.
Староста: "А ну, слезай, колдун! На тебя жалоба есть! За бесчинства ответишь!"
* [Приказать дубинке]
    ~ score_classic += 1
    ~ magic_distortion += 5
    Емеля: "Ну, как хотите. По щучьему веленью... дубинка, обломай-ка им бока!" #З
    Тяжелая дубовая палица сама взлетает с воза и с гулким свистом начинает колотить стражников.
* [Приказать дровам]
    ~ score_inventor += 1
    ~ magic_distortion += 2
    Емеля: "Мне с вами драться неохота. По щучьему веленью... дрова, станьте забором!" #Н
    Поленья с воза взлетают в воздух и кружат вокруг саней, так что стражники не смогли пробиться, как не старались.
* [Приказать стражникам]
    ~ score_storyteller += 1
    ~ magic_distortion += 5
    Емеля: (с усмешкой) "А ну-ка, согрейтесь! По щучьему веленью... стражники, пляшите до упаду!" #Р
    Стражники и сам староста вдруг пускаются в дикую, нелепую пляску.
-
В тот миг, когда магия сработала, у Емели резко зазвенело в ушах, а в глазах на секунду потемнело. Он тряхнул головой, отгоняя дурноту.
Емеля спокойно проезжает мимо. Униженный староста отправляет срочного гонца в столицу, к царю.
-> Act1_Scene8_Envoy


=== Act1_Scene8_Envoy ===
#Location: Izba
#clear
Прошла неделя. К избе Емели подъезжает расписная карета, из которой выходит боярин в собольей шапке — царский вельможа.
Вельможа: "Ты будешь Емельян?"
Емеля: (лениво) "Ну я. А тебе что?" #Н
Вельможа: (хитро улыбаясь) "Дошла до государя весть о твоей силе. И есть у него к тебе дело. Дочь его, царевна Марья, поражена неведомой хворью — не смеется. Царь зовет тебя первым попытать счастья. Великая награда ждет!"
* [Согласиться]
    Емеля: "Что ж, дело хорошее. Царевну жалко. Поеду." #Р
-
Вельможа: "Вот и славно! Собирайся, карета ждет."
Емеля: "Мне и тут тепло. Поезжай вперед, я за тобой буду." #Н
Вельможа, ошеломленный, но помня приказ "действовать ласково", кланяется и уезжает.
-> Act1_Scene9_Stove_Journey


=== Act1_Scene9_Stove_Journey ===
#Location: Courtyard
#clear
Емеля вылезает на двор.
Емеля: "По щучьему веленью... ступай, печь, к царю во дворец!" #Н
~ magic_distortion += 8
С оглушительным треском стена избы разваливается, и печь, пыхтя дымом, выезжает на улицу.
По всей деревне в избах сами собой затрещали и посыпались угли в очагах. Люди в страхе выбегают на улицу.
#Location: Road
#clear
Печь мчится по заснеженной дороге.
{ - score_inventor > score_classic and score_inventor > score_storyteller:
        Емеля успел заметить, что перед вревращением в печь влетела жестяная бадейка с водой. Голос Щуки в голове: "Сила не берется из ничего. Хочешь ехать быстрее — сделай жар сильнее."
}
На дороге его пытается обогнать тройка лихого купца.
* [Поддать жару и обогнать]
    { - score_inventor > 3:
        // Изобретатель знает, что делать
        Емеля потратил немного времени, чтобы подкинуть дров в печь. Внутри печи благодарно заурчало.
    - else:
        Емеля шепнул "по щучьему велению, поддай печка жару. Что-то недовольно и громко свиснуло, но печь припустила прытче."
        ~ magic_distortion += 1
    }
    Печь, сыто взревев и выдохнув клуб пара, с легкостью обгоняет тройку.
* [Пропустить]
-
#Location: Inn_Exterior
#clear
Проехав еще немного, Емеля чувствует, что проголодался.
Емеля: "Печь, стой у того крыльца!" #Н
Печь останавливается. На крыльцо выскакивает усатый трактирщик с ухватом наперевес.
Трактирщик: "Ч-что это за чудо-юдо?! Свят, свят, свят!"
* [Добыть угощение магией] (Классический)
    ~ score_classic += 1
    ~ humiliated_innkeeper = true
    Емеля, видя, что трактирщик напуган, решает не спорить. Он ухмыляется и, пока хозяин протирает глаза, тихо шепчет:
    Емеля: "По щучьему веленью... кувшин с квасом да тарелка с пирогами, явитесь ко мне на печь." #Н
    ~ magic_distortion += 3
    На глазах у ошарашенного трактирщика из дверей трактира сами собой вылетают кувшин и тарелка и плавно опускаются на лежанку. Трактирщик роняет ухват и крестится.
    -> continue_journey

* [Предложить инженерное решение] (Изобретатель)
    ~ score_inventor += 1
    ~ fixed_thing_for_innkeeper = true
    Емеля: (прищурившись, смотрит на трубу) "У тебя, хозяин, труба неправильно сложена. Тяга плохая. Дай-ка мне глины да пару кирпичей, я мигом исправлю. А ты меня за то обедом накормишь." #Н
    Ошарашенный трактирщик соглашается. Емеля, кряхтя, слезает с печи и, взобравшись на крышу, за несколько минут ловко перекладывает оголовок трубы. Дым, который до этого валил в избу, тут же столбом устремляется в небо.
    // МАГИИ НЕТ. DISTORTION = 0. Победа чистого ума.
    Изумленный и благодарный трактирщик выносит ему лучшую порцию щей и каши и с уважением смотрит на Емелю.
    -> continue_journey

* [Превратить страх в смех] (Сказитель)
    ~ score_storyteller += 1
    ~ impressed_innkeeper = true
    Емеля: "Не бойся, хозяин! Это карета царская, с отоплением! Слыхал историю про пляшущего старосту? Коли расскажу — с тебя угощение!" #Р
    Заинтригованный трактирщик соглашается. После уморительного рассказа он и постояльцы хохочут до слез.
    // МАГИИ НЕТ. Победа слова.
    Благодарный хозяин выносит Емеле пирогов и медовухи.
    -> continue_journey

= continue_journey
Подкрепившись, Емеля кивает трактирщику на прощание.
Емеля: "Ну, трогай, печка!" #Н
Печь, сыто пыхнув дымом, продолжает свой путь.
#Location: Palace_Exterior
#clear
К вечеру, обогнав вельможу, печь Емели с грохотом въезжает на дворцовую площадь. Стража у ворот в ужасе шарахается в стороны. Емеля впервые видит огромные белокаменные стены царского дворца, сияющие в лучах заходящего солнца.

Конец Акта I

Продолжение следует...

-> END